dnl Process this file with autoconf to produce a configure script.

AC_INIT(gaphor, 0.0.1)

AC_CONFIG_SRCDIR([gaphor/gaphor.py])
AM_CONFIG_HEADER(config.h)

dnl This is the only place where the package version appears
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

dnl put the ACLOCAL flags in the makefile
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AM_MAINTAINER_MODE

AC_PROG_CC

AM_PROG_LIBTOOL


#########
# Python

AM_PATH_PYTHON

AC_MSG_CHECKING(for python >= 2.2)
prog="
import sys, string
minver = (2,2,0,'final',0)
if sys.version_info < minver:
	sys.exit(1)
sys.exit(0)"
if $PYTHON -c "$prog" 1>&AC_FD_CC 2>&AC_FD_CC; then
	AC_MSG_RESULT(okay)
else
	AC_MSG_ERROR(too old)
fi

AM_CHECK_PYTHON_HEADERS

AM_CHECK_PYMOD(types, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(sys, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(xmllib, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(copy, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(gobject, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(gtk, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(gnome, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(gnome.canvas, , , AC_MSG_ERROR(This Python module is required))
AM_CHECK_PYMOD(diacanvas, , , AC_MSG_ERROR(This Python module is required))


##########
# C stuff

PKG_CHECK_MODULES(DIACANVAS2, diacanvas2 >= 0.5.0, \
			AC_MSG_RESULT(okay), \
			AC_MSG_ERROR(Can't find DiaCanvas2))
AC_SUBST(DIACANVAS2_CFLAGS)
AC_SUBST(DIACANVAS2_LIBS)

# PyGTK 

PKG_CHECK_MODULES(PYGTK, [pygtk-2.0 >= 1.99.6], \
			AC_MSG_RESULT(okay),
			AC_MSG_ERROR(Can't find PyGTK))
AC_SUBST(PYGTK_CFLAGS)

AC_PATH_PROG(PYGTK_CODEGEN, pygtk-codegen-2.0, no)
if test "x$PYGTK_CODEGEN" = xno; then
	AC_MSG_RESULT(could not find pygtk-codegen-2.0 script)
fi

AC_MSG_CHECKING(for pygtk defs)
PYGTK_DEFSDIR=`$PKG_CONFIG --variable=defsdir pygtk-2.0`
AC_SUBST(PYGTK_DEFSDIR)
AC_MSG_RESULT($PYGTK_DEFSDIR)


#########
# Output

AC_CONFIG_FILES(
	Makefile
	utils/Makefile
	gaphor/Makefile
	gaphor/UML/Makefile
	gaphor/diagram/Makefile
	gaphor/tree/Makefile
	gaphor/ui/Makefile
	tests/Makefile
	doc/Makefile)
AC_OUTPUT
